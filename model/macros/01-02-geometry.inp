!---------------------------------------
! MODEL GEOMETRY
!---------------------------------------

! computed values
!---------------------------------------

! rise bolt locations
SPS_RBOLTSN = (SPS_L-2*SPS_RBOLTE)/SPS_RBOLTS-mod((SPS_L-2*SPS_RBOLTE)/SPS_RBOLTS,1)-1
SPS_RBOLT2 = (SPS_L-2*SPS_RBOLTE-SPS_RBOLTSN*SPS_RBOLTS)/2

SPS_RBOLTN = 2+SPS_RBOLTSN+1
*dim,SPS_RBOLTX,array,SPS_RBOLTN
*set,SPS_RBOLTX(1),SPS_RBOLTE
*do,I,0,SPS_RBOLTSN
    *set,SPS_RBOLTX(1+I+1),SPS_RBOLTE+SPS_RBOLT2+I*SPS_RBOLTS
*enddo
*set,SPS_RBOLTX(SPS_RBOLTN),SPS_L-SPS_RBOLTE
SPS_RBOLTSN=
SPS_RBOLT2=


! seat bracket locations
BRK_SN = (SPS_L-BRK_END)/BRK_SPC-mod((SPS_L-BRK_END)/BRK_SPC,1)-1
*if,BRK_END,ne,0,then
    BRK_1 = BRK_END
*else
    BRK_1 = (SPS_L-BRK_SN*BRK_SPC)/2
*endif

BRK_N = BRK_SN+1
*dim,BRK_X,array,BRK_N
*do,I,0,BRK_SN
    *set,BRK_X(I+1),BRK_1+I*BRK_SPC
*enddo
BRK_SN=
BRK_1=



! sps
!---------------------------------------

! top plate part 1
*get,KP0,kp,,num,max
k,KP0+1,0,SPS_W+SPS_TP,SPS_W*SPS_S+SPS_LAP
k,KP0+2,0,SPS_W+SPS_TP,SPS_W*SPS_S+(1.5*SPS_TP)*tan(45-atan(SPS_S)/2)
k,KP0+3,0,SPS_W,SPS_W*SPS_S+(1.5*SPS_TP)*tan(45-atan(SPS_S)/2)
k,KP0+4,0,SPS_W,SPS_W*SPS_S+SPS_LAP

lsel,none
l,KP0+1,KP0+2
l,KP0+2,KP0+3
l,KP0+3,KP0+4
l,KP0+4,KP0+1

asel,none
al,all
cm,A_VOL_SPS_TP_1,area


! top plate part 2
*get,KP0,kp,,num,max
k,KP0+1,0,SPS_W-(1.5*SPS_TP),SPS_W*SPS_S+(1.5*SPS_TP)*tan(45-atan(SPS_S)/2) ! arc centre
k,KP0+2,0,SPS_W+SPS_TP,SPS_W*SPS_S+(1.5*SPS_TP)*tan(45-atan(SPS_S)/2)
k,KP0+3,0,SPS_W-(1.5*SPS_TP)+(2.5*SPS_TP)*sin(atan(SPS_S)),SPS_W*SPS_S+(1.5*SPS_TP)*tan(45-atan(SPS_S)/2)-(2.5*SPS_TP)*cos(atan(SPS_S))
k,KP0+4,0,SPS_W-(1.5*SPS_TP)+(1.5*SPS_TP)*sin(atan(SPS_S)),SPS_W*SPS_S+(1.5*SPS_TP)*tan(45-atan(SPS_S)/2)-(1.5*SPS_TP)*cos(atan(SPS_S))
k,KP0+5,0,SPS_W,SPS_W*SPS_S+(1.5*SPS_TP)*tan(45-atan(SPS_S)/2)

lsel,none
larc,KP0+2,KP0+3,KP0+1,2.5*SPS_TP
l,KP0+3,KP0+4
larc,KP0+4,KP0+5,KP0+1,1.5*SPS_TP
l,KP0+5,KP0+2

asel,none
al,all
cm,A_VOL_SPS_TP_2,area


! top plae part 3
*get,KP0,kp,,num,max
k,KP0+1,0,SPS_W-(1.5*SPS_TP)+(1.5*SPS_TP)*sin(atan(SPS_S)),SPS_W*SPS_S+(1.5*SPS_TP)*tan(45-atan(SPS_S)/2)-(1.5*SPS_TP)*cos(atan(SPS_S))
k,KP0+2,0,SPS_W-(1.5*SPS_TP)+(2.5*SPS_TP)*sin(atan(SPS_S)),SPS_W*SPS_S+(1.5*SPS_TP)*tan(45-atan(SPS_S)/2)-(2.5*SPS_TP)*cos(atan(SPS_S))
k,KP0+3,0,2.5*SPS_TP-(1.5*SPS_TP)*sin(atan(SPS_S)),-(2.5*SPS_TP)*tan(45-atan(SPS_S)/2)+(1.5*SPS_TP)*cos(atan(SPS_S))
k,KP0+4,0,2.5*SPS_TP-(2.5*SPS_TP)*sin(atan(SPS_S)),-(2.5*SPS_TP)*tan(45-atan(SPS_S)/2)+(2.5*SPS_TP)*cos(atan(SPS_S))
lsel,none

l,KP0+1,KP0+2
l,KP0+2,KP0+3
l,KP0+3,KP0+4
l,KP0+4,KP0+1

asel,none
al,all
wprota,0,-90,0
wpoffs,,,SPS_W/2
asbw,all,sepo
wpcsys
cm,A_VOL_SPS_TP_3,area


! top plate part 4
*get,KP0,kp,,num,max
k,KP0+1,0,2.5*SPS_TP,-(2.5*SPS_TP)*tan(45-atan(SPS_S)/2) ! arc centre
k,KP0+2,0,2.5*SPS_TP-(1.5*SPS_TP)*sin(atan(SPS_S)),-(2.5*SPS_TP)*tan(45-atan(SPS_S)/2)+(1.5*SPS_TP)*cos(atan(SPS_S))
k,KP0+3,0,SPS_TP,-(2.5*SPS_TP)*tan(45-atan(SPS_S)/2)
k,KP0+4,0,0,-(2.5*SPS_TP)*tan(45-atan(SPS_S)/2)
k,KP0+5,0,2.5*SPS_TP-(2.5*SPS_TP)*sin(atan(SPS_S)),-(2.5*SPS_TP)*tan(45-atan(SPS_S)/2)+(2.5*SPS_TP)*cos(atan(SPS_S))

lsel,none
larc,KP0+2,KP0+3,KP0+1,1.5*SPS_TP
l,KP0+3,KP0+4
larc,KP0+4,KP0+5,KP0+1,2.5*SPS_TP
l,KP0+5,KP0+2

asel,none
al,all
cm,A_VOL_SPS_TP_4,area


! top plate part 5
*get,KP0,kp,,num,max
k,KP0+1,0,SPS_TP,-(2.5*SPS_TP)*tan(45-atan(SPS_S)/2)
k,KP0+2,0,SPS_TP,-SPS_H+SPS_W*SPS_S+(SPS_LAP+SPS_GAP)
k,KP0+3,0,0,-SPS_H+SPS_W*SPS_S+(SPS_LAP+SPS_GAP)
k,KP0+4,0,0,-(2.5*SPS_TP)*tan(45-atan(SPS_S)/2)

lsel,none
l,KP0+1,KP0+2
l,KP0+2,KP0+3
l,KP0+3,KP0+4
l,KP0+4,KP0+1

asel,none
al,all
wprota,0,0,0
wpoffs,,,(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(1.5*SPS_BP)*tan(45-atan(SPS_S)/2)
asbw,all
wpcsys
wprota,0,0,0
wpoffs,,,(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-SPS_LAP
asbw,all
wpcsys
wprota,0,0,0
wpoffs,,,(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(SPS_LAP+SPS_GAP)
asbw,all
wpcsys
wprota,0,0,0
wpoffs,,,-SPS_H+SPS_S*SPS_W+BRK_BZ
asbw,all
*if,BRK_BSV,ne,0,then
    wpoffs,,,BRK_BSV
    asbw,all
*endif
wpcsys
cm,A_VOL_SPS_TP_5,area


! core part 1
*get,KP0,kp,,num,max
k,KP0+1,0,SPS_W-(1.5*SPS_TP),SPS_W*SPS_S+(1.5*SPS_TP)*tan(45-atan(SPS_S)/2) ! centre
k,KP0+2,0,SPS_W+SPS_TP+SPS_BP-(2.5*SPS_BP),(SPS_W+SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))+(2.5*SPS_BP)*tan(45-atan(SPS_S)/2)
k,KP0+3,0,SPS_W+SPS_TP,SPS_W*SPS_S+(1.5*SPS_TP)*tan(45-atan(SPS_S)/2)
k,KP0+4,0,SPS_W+SPS_TP,(SPS_W+SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))+(2.5*SPS_BP)*tan(45-atan(SPS_S)/2)
k,KP0+5,0,SPS_W+SPS_TP+SPS_BP-(2.5*SPS_BP)+(1.5*SPS_BP)*sin(atan(SPS_S)),(SPS_W+SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))+(2.5*SPS_BP)*tan(45-atan(SPS_S)/2)-(1.5*SPS_BP)*cos(atan(SPS_S))
k,KP0+6,0,SPS_W-(1.5*SPS_TP)+(2.5*SPS_TP)*sin(atan(SPS_S)),SPS_W*SPS_S+(1.5*SPS_TP)*tan(45-atan(SPS_S)/2)-(2.5*SPS_TP)*cos(atan(SPS_S))

lsel,none
l,KP0+3,KP0+4
larc,KP0+4,KP0+5,KP0+2,1.5*SPS_BP
l,KP0+5,KP0+6
larc,KP0+6,KP0+3,KP0+1,2.5*SPS_TP

asel,none
al,all
cm,A_VOL_SPS_CORE_1,area


! top plate
asel,none
*do,H,1,5
    cmsel,a,A_VOL_SPS_TP_%H%
*enddo
cm,A_VOL_SPS_TP,area


! core part 2
*get,KP0,kp,,num,max
k,KP0+1,0,SPS_W-(1.5*SPS_TP)+(2.5*SPS_TP)*sin(atan(SPS_S)),SPS_W*SPS_S+(1.5*SPS_TP)*tan(45-atan(SPS_S)/2)-(2.5*SPS_TP)*cos(atan(SPS_S))
k,KP0+2,0,SPS_W+SPS_TP+SPS_BP-(2.5*SPS_BP)+(1.5*SPS_BP)*sin(atan(SPS_S)),(SPS_W+SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))+(2.5*SPS_BP)*tan(45-atan(SPS_S)/2)-(1.5*SPS_BP)*cos(atan(SPS_S))
k,KP0+4,0,2.5*SPS_TP-(1.5*SPS_TP)*sin(atan(SPS_S)),-(2.5*SPS_TP)*tan(45-atan(SPS_S)/2)+(1.5*SPS_TP)*cos(atan(SPS_S))
k,KP0+3,0,SPS_TP+SPS_BP+(1.5*SPS_BP)-(2.5*SPS_BP)*sin(atan(SPS_S)),(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(1.5*SPS_BP)*tan(45-atan(SPS_S)/2)+(2.5*SPS_BP)*cos(atan(SPS_S))

lsel,none
l,KP0+1,KP0+2
l,KP0+2,KP0+3
l,KP0+3,KP0+4
l,KP0+4,KP0+1

asel,none
al,all
wprota,0,-90,0
wpoffs,,,SPS_W/2
asbw,all,sepo
wpcsys
cm,A_VOL_SPS_CORE_2,area


! core part 3
*get,KP0,kp,,num,max
k,KP0+1,0,2.5*SPS_TP,-(2.5*SPS_TP)*tan(45-atan(SPS_S)/2) ! centre
k,KP0+2,0,SPS_TP+SPS_BP+(1.5*SPS_BP),(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(1.5*SPS_BP)*tan(45-atan(SPS_S)/2) ! centre
k,KP0+3,0,2.5*SPS_TP-(1.5*SPS_TP)*sin(atan(SPS_S)),-(2.5*SPS_TP)*tan(45-atan(SPS_S)/2)+(1.5*SPS_TP)*cos(atan(SPS_S))
k,KP0+4,0,SPS_TP+SPS_BP+(1.5*SPS_BP)-(2.5*SPS_BP)*sin(atan(SPS_S)),(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(1.5*SPS_BP)*tan(45-atan(SPS_S)/2)+(2.5*SPS_BP)*cos(atan(SPS_S))
k,KP0+5,0,SPS_TP,(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(1.5*SPS_BP)*tan(45-atan(SPS_S)/2)
k,KP0+6,0,SPS_TP,-(2.5*SPS_TP)*tan(45-atan(SPS_S)/2)

lsel,none
l,KP0+3,KP0+4
larc,KP0+4,KP0+5,KP0+2,2.5*SPS_BP
l,KP0+5,KP0+6
larc,KP0+6,KP0+3,KP0+1,1.5*SPS_TP

asel,none
al,all
cm,A_VOL_SPS_CORE_3,area


! core
asel,none
*do,H,1,3
    cmsel,a,A_VOL_SPS_CORE_%H%
*enddo
cm,A_VOL_SPS_CORE,area


! bottom plate part 1
*get,KP0,kp,,num,max
k,KP0+1,0,SPS_W+SPS_TP+SPS_BP,SPS_H+(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(SPS_LAP+SPS_GAP)
k,KP0+2,0,SPS_W+SPS_TP+SPS_BP,(SPS_W+SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))+(2.5*SPS_BP)*tan(45-atan(SPS_S)/2)
k,KP0+3,0,SPS_W+SPS_TP,(SPS_W+SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))+(2.5*SPS_BP)*tan(45-atan(SPS_S)/2)
k,KP0+4,0,SPS_W+SPS_TP,SPS_H+(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(SPS_LAP+SPS_GAP)

lsel,none
l,KP0+1,KP0+2
l,KP0+2,KP0+3
l,KP0+3,KP0+4
l,KP0+4,KP0+1

asel,none
al,all
wprota,0,0,0
wpoffs,,,SPS_W*SPS_S+(1.5*SPS_TP)*tan(45-atan(SPS_S)/2)
asbw,all
wpcsys
wprota,0,0,0
wpoffs,,,SPS_W*SPS_S+SPS_LAP
asbw,all
wpcsys
wprota,0,0,0
wpoffs,,,SPS_W*SPS_S+(SPS_LAP+SPS_GAP)
asbw,all
wpcsys
wprota,0,0,0
wpoffs,,,SPS_S*SPS_W+BRK_BZ
asbw,all
*if,BRK_BSV,ne,0,then
    wpoffs,,,BRK_BSV
    asbw,all
*endif
wpcsys
cm,A_VOL_SPS_BP_1,area


! bottom plate part 2
*get,KP0,kp,,num,max
k,KP0+1,0,SPS_W+SPS_TP+SPS_BP-(2.5*SPS_BP),(SPS_W+SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))+(2.5*SPS_BP)*tan(45-atan(SPS_S)/2) ! centre
k,KP0+2,0,SPS_W+SPS_TP+SPS_BP,(SPS_W+SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))+(2.5*SPS_BP)*tan(45-atan(SPS_S)/2)
k,KP0+3,0,SPS_W+SPS_TP+SPS_BP-(2.5*SPS_BP)+(2.5*SPS_BP)*sin(atan(SPS_S)),(SPS_W+SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))+(2.5*SPS_BP)*tan(45-atan(SPS_S)/2)-(2.5*SPS_BP)*cos(atan(SPS_S))
k,KP0+4,0,SPS_W+SPS_TP+SPS_BP-(2.5*SPS_BP)+(1.5*SPS_BP)*sin(atan(SPS_S)),(SPS_W+SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))+(2.5*SPS_BP)*tan(45-atan(SPS_S)/2)-(1.5*SPS_BP)*cos(atan(SPS_S))
k,KP0+5,0,SPS_W+SPS_TP,(SPS_W+SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))+(2.5*SPS_BP)*tan(45-atan(SPS_S)/2)

lsel,none
larc,KP0+2,KP0+3,KP0+1,2.5*SPS_BP
l,KP0+3,KP0+4
larc,KP0+4,KP0+5,KP0+1,1.5*SPS_BP
l,KP0+5,KP0+2

asel,none
al,all
cm,A_VOL_SPS_BP_2,area


! bottom plate part 3
*get,KP0,kp,,num,max
k,KP0+1,0,SPS_W+SPS_TP+SPS_BP-(2.5*SPS_BP)+(1.5*SPS_BP)*sin(atan(SPS_S)),(SPS_W+SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))+(2.5*SPS_BP)*tan(45-atan(SPS_S)/2)-(1.5*SPS_BP)*cos(atan(SPS_S))
k,KP0+2,0,SPS_W+SPS_TP+SPS_BP-(2.5*SPS_BP)+(2.5*SPS_BP)*sin(atan(SPS_S)),(SPS_W+SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))+(2.5*SPS_BP)*tan(45-atan(SPS_S)/2)-(2.5*SPS_BP)*cos(atan(SPS_S))
k,KP0+3,0,SPS_TP+SPS_BP+(1.5*SPS_BP)-(1.5*SPS_BP)*sin(atan(SPS_S)),(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(1.5*SPS_BP)*tan(45-atan(SPS_S)/2)+(1.5*SPS_BP)*cos(atan(SPS_S))
k,KP0+4,0,SPS_TP+SPS_BP+(1.5*SPS_BP)-(2.5*SPS_BP)*sin(atan(SPS_S)),(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(1.5*SPS_BP)*tan(45-atan(SPS_S)/2)+(2.5*SPS_BP)*cos(atan(SPS_S))

lsel,none
l,KP0+1,KP0+2
l,KP0+2,KP0+3
l,KP0+3,KP0+4
l,KP0+4,KP0+1

asel,none
al,all
wprota,0,-90,0
wpoffs,,,SPS_W/2
asbw,all,sepo
wpcsys
cm,A_VOL_SPS_BP_3,area


! bottom plate part 4
*get,KP0,kp,,num,max
k,KP0+1,0,SPS_TP+SPS_BP+(1.5*SPS_BP),(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(1.5*SPS_BP)*tan(45-atan(SPS_S)/2) ! centre
k,KP0+2,0,SPS_TP+SPS_BP+(1.5*SPS_BP)-(1.5*SPS_BP)*sin(atan(SPS_S)),(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(1.5*SPS_BP)*tan(45-atan(SPS_S)/2)+(1.5*SPS_BP)*cos(atan(SPS_S))
k,KP0+3,0,SPS_TP+SPS_BP,(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(1.5*SPS_BP)*tan(45-atan(SPS_S)/2)
k,KP0+4,0,SPS_TP,(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(1.5*SPS_BP)*tan(45-atan(SPS_S)/2)
k,KP0+5,0,SPS_TP+SPS_BP+(1.5*SPS_BP)-(2.5*SPS_BP)*sin(atan(SPS_S)),(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(1.5*SPS_BP)*tan(45-atan(SPS_S)/2)+(2.5*SPS_BP)*cos(atan(SPS_S))

lsel,none
larc,KP0+2,KP0+3,KP0+1,1.5*SPS_BP
l,KP0+3,KP0+4
larc,KP0+4,KP0+5,KP0+1,2.5*SPS_BP
l,KP0+5,KP0+2

asel,none
al,all
cm,A_VOL_SPS_BP_4,area


! bottom plate part 5
*get,KP0,kp,,num,max
k,KP0+1,0,SPS_TP+SPS_BP,(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(1.5*SPS_BP)*tan(45-atan(SPS_S)/2)
k,KP0+2,0,SPS_TP+SPS_BP,(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-SPS_LAP
k,KP0+3,0,SPS_TP,(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-SPS_LAP
k,KP0+4,0,SPS_TP,(SPS_TP+SPS_BP)*SPS_S-(SPS_TP+SPS_CORE+SPS_BP)/cos(atan(SPS_S))-(1.5*SPS_BP)*tan(45-atan(SPS_S)/2)

lsel,none
l,KP0+1,KP0+2
l,KP0+2,KP0+3
l,KP0+3,KP0+4
l,KP0+4,KP0+1

asel,none
al,all
cm,A_VOL_SPS_BP_5,area


! bottom plate
asel,none
*do,H,1,5
    cmsel,a,A_VOL_SPS_BP_%H%
*enddo
cm,A_VOL_SPS_BP,area


! extrude
*do,H,1,5
    asel,none
    cmsel,a,A_VOL_SPS_TP_%H%
    vsel,none
    vext,all,,,SPS_L

    *do,I,1,BRK_N
        wprota,90,90,0
        wpoffs,,,BRK_X(I)-BRK_BSH/2
        vsbw,all
        *if,BRK_BSH,ne,0,then
            wpoffs,,,BRK_BSH
            vsbw,all
        *endif
        wpcsys
    *enddo

    cm,V_SPS_TP_%H%,volu
*enddo

*do,H,1,3
    asel,none
    cmsel,a,A_VOL_SPS_CORE_%H%
    vsel,none
    vext,all,,,SPS_L

    *do,I,1,BRK_N
        wprota,90,90,0
        wpoffs,,,BRK_X(I)-BRK_BSH/2
        vsbw,all
        *if,BRK_BSH,ne,0,then
            wpoffs,,,BRK_BSH
            vsbw,all
        *endif
        wpcsys
    *enddo

    cm,V_SPS_CORE_%H%,volu
*enddo

*do,H,1,5
    asel,none
    cmsel,a,A_VOL_SPS_BP_%H%
    vsel,none
    vext,all,,,SPS_L

    *do,I,1,BRK_N
        wprota,90,90,0
        wpoffs,,,BRK_X(I)-BRK_BSH/2
        vsbw,all
        *if,BRK_BSH,ne,0,then
            wpoffs,,,BRK_BSH
            vsbw,all
        *endif
        wpcsys
    *enddo

    cm,V_SPS_BP_%H%,volu
*enddo


! sps component
!---------------------------------------

! terrace
vsel,none
*do,H,1,5
    cmsel,a,V_SPS_TP_%H%
*enddo
vatt,MAT_STEEL,RCO_DUMMY,ETY_SOLID
cm,V_SPS_TP,volu

vsel,none
*do,H,1,3
    cmsel,a,V_SPS_CORE_%H%
*enddo
vatt,MAT_ELAST,RCO_DUMMY,ETY_SOLID
cm,V_SPS_CORE,volu

vsel,none
*do,H,1,5
    cmsel,a,V_SPS_BP_%H%
*enddo
vatt,MAT_STEEL,RCO_DUMMY,ETY_SOLID
cm,V_SPS_BP,volu

vsel,none
cmsel,a,V_SPS_TP
cmsel,a,V_SPS_CORE
cmsel,a,V_SPS_BP
cm,V_SPS,volu


! generate and merge geometry
cmsel,s,V_SPS
vsel,r,loc,y,0,SPS_W/2
vgen,1,all,,,0,SPS_W,SPS_H,,,1

vsel,all
aslv
lsla
ksll
nummrg,kp,TOL



! seat brackets
!---------------------------------------

! baseplate
*get,KP0,kp,,num,max
k,KP0+1,BRK_X(1)-BRK_W/2,SPS_W,SPS_S*SPS_W+BRK_Z
k,KP0+2,BRK_X(1)+BRK_W/2,SPS_W,SPS_S*SPS_W+BRK_Z
k,KP0+3,BRK_X(1)+BRK_W/2,SPS_W,SPS_S*SPS_W+BRK_Z+BRK_H
k,KP0+4,BRK_X(1)-BRK_W/2,SPS_W,SPS_S*SPS_W+BRK_Z+BRK_H

lsel,none
l,KP0+1,KP0+2
l,KP0+2,KP0+3
l,KP0+3,KP0+4
l,KP0+4,KP0+1

asel,none
al,all
wprota,90,90,0
wpoffs,,,BRK_X(1)-BRK_BSH/2
asbw,all
*if,BRK_BSH,ne,0,then
    wpoffs,,,BRK_BSH
    asbw,all
*endif
wpcsys
wprota,0,0,0
wpoffs,,,SPS_S*SPS_W+BRK_BZ
asbw,all
*if,BRK_BSV,ne,0,then
    wpoffs,,,BRK_BSV
    asbw,all
*endif
wpcsys
*if,BRK_BSH,eq,0,then
    wprota,90,90,0
    wpoffs,,,BRK_X(1)-BRK_W/2+BRK_ST_T/2
    asbw,all
    wpoffs,,,BRK_ST_T/2
    asbw,all
    wpcsys
*else
    wprota,90,90,0
    wpoffs,,,BRK_X(1)-BRK_ST_T/2
    asbw,all
    wpoffs,,,BRK_ST_T/2
    asbw,all
    wpoffs,,,BRK_ST_T/2
    asbw,all
    wpcsys
*endif
cm,A_VOL_BRK_PL,area


! stanchion
*get,KP0,kp,,num,max
*if,BRK_BSH,eq,0,then
    k,KP0+1,BRK_X(1)-BRK_W/2,SPS_W-BRK_T,SPS_S*SPS_W+BRK_Z
    k,KP0+2,BRK_X(1)-BRK_W/2,SPS_W-BRK_ST_L,SPS_S*SPS_W+BRK_Z
    k,KP0+3,BRK_X(1)-BRK_W/2,SPS_W-BRK_ST_L,SPS_S*SPS_W+BRK_Z+BRK_H
    k,KP0+4,BRK_X(1)-BRK_W/2,SPS_W-BRK_T,SPS_S*SPS_W+BRK_Z+BRK_H
*else
    k,KP0+1,BRK_X(1)-BRK_ST_T/2,SPS_W-BRK_T,SPS_S*SPS_W+BRK_Z
    k,KP0+2,BRK_X(1)-BRK_ST_T/2,SPS_W-BRK_ST_L,SPS_S*SPS_W+BRK_Z
    k,KP0+3,BRK_X(1)-BRK_ST_T/2,SPS_W-BRK_ST_L,SPS_S*SPS_W+BRK_Z+BRK_H
    k,KP0+4,BRK_X(1)-BRK_ST_T/2,SPS_W-BRK_T,SPS_S*SPS_W+BRK_Z+BRK_H
*endif

lsel,none
l,KP0+1,KP0+2
l,KP0+2,KP0+3
l,KP0+3,KP0+4
l,KP0+4,KP0+1

asel,none
al,all
cm,A_VOL_BRK_ST,area


! extrude
asel,none
cmsel,a,A_VOL_BRK_PL
vsel,none
vext,all,,,,-BRK_T
cm,V_BRK_PL,volu

asel,none
cmsel,a,A_VOL_BRK_ST
vsel,none
vext,all,,,BRK_ST_T
*if,BRK_BSH,ne,0,then
    wprota,90,90,0
    wpoffs,,,BRK_X(1)
    vsbw,all
    wpcsys
*endif
cm,V_BRK_ST,volu


! component
vsel,none
cmsel,a,V_BRK_PL
cmsel,a,V_BRK_ST
aslv
lsla
ksll
nummrg,kp,TOL
vatt,MAT_RIGID,RCO_DUMMY,ETY_SOLID
cm,V_BRK_1,volu


! generate geometry
*do,I,2,BRK_N
    vsel,none
    cmsel,a,V_BRK_1
    vgen,2,all,,,BRK_X(I)-BRK_X(1)
    cmsel,u,V_BRK_1
    cm,V_BRK_%I%,volu
*enddo
